' Module
' Coded in VBA
' Used for 2 buttons (Col D / Col K)

Option Explicit

' ===== CONSTANTS =====

Const PROTECT_PWD As String = "OnbKYC"            

' Source input ranges
Const RANGE_D_INPUT As String = "D6:D11"
Const RANGE_K_INPUT As String = "K6:K10"

' Clear ranges
Const CLEAR_D As String = "D9,D10,D12"
Const CLEAR_K As String = "K9:K12"

Const START_ROW As Long = 5

Const COL_D As Long = 4
Const COL_K As Long = 11

Const STATUS_SHEET As String = "Information"
Const STATUS_CELL As String = "H1"

Const QUANT_CELL As String = "D11"

' ===== CORE SUB =====

Private Sub WriteToTable( _
    sourceName As String, _
    targetName As String, _
    tblName As String, _
    inputRange As String, _
    sourceCol As Long, _
    clearRange As String)

    Dim wsSource As Worksheet, wsTarget As Worksheet
    Dim tbl As ListObject, newRow As ListRow
    Dim cell As Range
    Dim i As Long

    ' Check if already running
    If Sheets(STATUS_SHEET).Range(STATUS_CELL).Value = "Running" Then
        MsgBox "Process already running", vbExclamation
        Exit Sub
    End If
    Sheets(STATUS_SHEET).Range(STATUS_CELL).Value = "Running"

    On Error GoTo ErrHandler

    ' Set objects
    Set wsSource = Sheets(sourceName)
    Set wsTarget = Sheets(targetName)
    Set tbl = wsTarget.ListObjects(tblName)

    ' Validate required fields
    For Each cell In wsSource.Range(inputRange)
        If Trim(cell.Value) = "" Then
            MsgBox "Required field blank: " & cell.Address(False, False), vbCritical
            GoTo SafeExit
        End If
    Next cell

    ' Unprotect + add row
    wsTarget.Unprotect PROTECT_PWD
    Set newRow = tbl.ListRows.Add

    ' Write 7 values
    For i = 1 To 7
        newRow.Range(1, i).Value = wsSource.Cells(START_ROW + i, sourceCol).Value
    Next i

    ' Clear inputs
    wsSource.Range(clearRange).ClearContents
    wsSource.Range(QUANT_CELL).Value = "1"

    MsgBox "Data successfully added.", vbInformation

SafeExit:
    On Error Resume Next
    wsTarget.Protect PROTECT_PWD
    Sheets(STATUS_SHEET).Range(STATUS_CELL).Value = ""
    Exit Sub

ErrHandler:
    MsgBox "Error: " & Err.Description, vbCritical
    Resume SafeExit

End Sub

' ===== INTIALISING SUBS ======

Sub BauUpload(sourceName As String, targetName As String, tblName As String)
    Call WriteToTable(sourceName, targetName, tblName, RANGE_D_INPUT, COL_D, CLEAR_D)
End Sub

Sub AdhocUpload(sourceName As String, targetName As String, tblName As String)
    Call WriteToTable(sourceName, targetName, tblName, RANGE_K_INPUT, COL_K, CLEAR_K)
End Sub


